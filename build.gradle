plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'test-report-aggregation'
}
apply from: 'version.gradle'

group = 'com.krasnopolskyi'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    all {                      ///////for logger
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "org.springframework.boot:spring-boot-starter-log4j2:${versions.log4j2}"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${versions.openapi}"

    implementation 'org.liquibase:liquibase-core'
    implementation 'org.postgresql:postgresql'

    implementation "org.projectlombok:lombok:${versions.lombok}"
    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:postgresql:${versions.postgresql}"
    testImplementation "org.testcontainers:junit-jupiter:${versions.testcontainers}"
}

tasks.named('test') {
    useJUnitPlatform {
        filter {
            includeTestsMatching "*Test"
            excludeTestsMatching "*IntegrationTest"
        }
    }
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

def integrationTest = tasks.register("integrationTest", Test) {
    useJUnitPlatform {
        filter {
            includeTestsMatching '*IntegrationTest'
        }
    }
    testLogging {
        events "passed", "skipped", "failed"
    }

}

tasks.named('check') {
    dependsOn integrationTest
}
